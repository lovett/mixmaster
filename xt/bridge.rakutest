#!/usr/bin/env rakudo

use File::Temp;
use Test;

my $mixmaster = $*PROGRAM.IO.parent(2).add('bin/mixmaster');
my $fixture = $*PROGRAM.IO.parent(2).add('t/fixture/gitea.json');
my $buildroot = tempdir;

run $mixmaster, qqw{--buildroot $buildroot setup}, :out;

my $job = $fixture.IO.slurp;

my $proc = run $mixmaster, qqw{--buildroot $buildroot bridge}, :in, :out, :err;
$proc.in.print: qq:to/REQUEST/;
POST / HTTP/1.0
Content-Type: application/json
Remote-Addr: 127.0.0.1
Connection: close
Content-Length: {$job.chars}

{$job}
REQUEST

my $stderr = $proc.err.slurp;

my IO::Path @files = $buildroot.IO.add("INBOX").dir(test => /'.' json $/);

plan 4;

is @files.elems, 1, "Creates job file in inbox";
is @files[0].slurp.chars, $job.chars, "Job file matches fixture";
like $stderr, /'New job received'/, "Logs job received to stderr";
is $proc.exitcode, 0, "Clean exit";

done-testing;
