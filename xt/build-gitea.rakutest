#!/usr/bin/env rakudo

use File::Temp;
use Test;

my $mixmaster = $*PROGRAM.IO.parent(2).add('bin/mixmaster');
my $job-fixture = $*PROGRAM.IO.parent(2).add('t/fixture/gitea.json');
my $config-fixture = $*PROGRAM.IO.parent(2).add('t/fixture/mixmaster.ini');
my $log-name = "gitea.log";
my $buildroot = tempdir;
my $workspace = $buildroot.IO.add("test-org-test-repo");
my $test-repo = "/tmp/test-repo";

run $mixmaster, qqw{--buildroot $buildroot setup}, :out;

copy($job-fixture, $buildroot.IO.add("INBOX").add($job-fixture.basename));

copy($config-fixture, $buildroot.IO.add($config-fixture.basename));

unless ($test-repo.IO.d) {
    run "git", qqw{init -q $test-repo};
    indir $test-repo, {
        run "git", <checkout -q -b production>;
        spurt $test-repo.IO.add("hello.txt"),  "hello world";
        run "git", <add hello.txt>;
        run "git", <commit -q -m Hello>;
    };
}

my $proc = run $mixmaster, qqw{--buildroot $buildroot build}, :out, :err;
$proc.out.close();

my $log = $workspace.add("ARCHIVE/{$log-name}");
my @log-lines = $log.IO.slurp;

plan 11;

is dir($buildroot.IO.add("INBOX")).elems, 0, "Job file was moved out of INBOX";
is dir($buildroot.IO.add("ARCHIVE")).elems, 1, "Job file was moved into ARCHIVE";
ok $workspace.d, "Workspace was created";
ok $workspace.add("ARCHIVE").d, "Workspace archive was created";
ok $workspace.add("production").d, "Workspace target directory was created";
ok $log.f, "Workspace log was created";
is @log-lines.grep(rx:s/\$ git clone/).elems, 1, "Git clone appeared in log";
is @log-lines.grep(rx:s/\$ git checkout/).elems, 1, "Git checkout appeared in log";
is @log-lines.grep(rx:s/\$ echo Stub command for building production branch/).elems, 1, "Build command was logged";
is @log-lines.grep(rx:s/O Stub command for building production branch/).elems, 1, "Build command output was logged";
is $proc.exitcode, 0, "Command exits zero";

done-testing;
