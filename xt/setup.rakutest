#!/usr/bin/env rakudo

use File::Temp;
use Test;

my $mixmaster = $*PROGRAM.IO.parent(2).add('bin/mixmaster');
my $buildroot = tempdir;
my $proc = run $mixmaster, qqw{--buildroot $buildroot setup}, :out, :err;
my $stdout = $proc.out.slurp: :close;
my $stderr = $proc.err.slurp: :close;

plan 8;

like $stdout, / 'Created ' $buildroot /, "Stdout mentions created paths";
like $stdout, / 'Populated ' $buildroot /, "Stdout mentions config population";
is $stderr, "", "Stderr is empty";
ok $buildroot.IO.d, "Creates buildroot";
ok $buildroot.IO.add("INBOX").d, "Creates INBOX directory in buildroot";
ok $buildroot.IO.add("ARCHIVE").d, "Creates ARCHIVE directory in buildroot";
ok $buildroot.IO.add("mixmaster.ini").f, "Creates mixmaster.ini in buildroot";
is $proc.exitcode, 0, "Clean exit";

done-testing;
