#!/usr/bin/env rakudo

=begin pod

This is the cleanup script for mixmaster. It deletes files from the
spool directory (default: C</var/spool/mixmaster/USER>) when the
systemd service fails to start.

In normal operation, C<mmbuild> moves files from the spool directory
to the build root. Cleanup is only necessary when a severe problem
occurs.

=end pod

use lib '/usr/local/share/mixmaster/lib';
use lib $*PROGRAM.absolute.IO.parent(2).add('lib');

use Config;

our Str constant SCRIPT_VERSION = "2020.05.04";

multi MAIN(Bool :$man) {
    run $*EXECUTABLE, '--doc', $*PROGRAM;
}

multi MAIN(Bool :$version) {
    say SCRIPT_VERSION;
    exit;
}

multi sub MAIN() {
    unless has-config() {
        exit;
    }

    my Hash %config{Str} = get-config();

    unless (%config<_><spool>.IO.d) {
        exit;
    }

    # See https://www.freedesktop.org/software/systemd/man/systemd.exec.html
    given %*ENV<SERVICE_RESULT> {
        when "success" {
            exit;
        }

        default {
            my IO::Path @jobs = dir(%config<_><spool>, test => /'.' ini $/);

            for (@jobs) {
                try unlink($_);
                say "Deleted {$_}";
            }
        }
    }
}

# Local Variables:
# mode: raku
# End:
