#!/usr/bin/env rakudo
# -*- mode: perl6 -*-

=begin pod

This is the setup script for mixmaster. It's responsible for
populating:

=item The application configuration file in C<~/.config>

=item The directory where builds occur, defaulting to C<~/Builds>

=item A per-user systemd socket service in C<~/.config/systemd/user>
      that listens for build requests and stores them a job files
      under C</var/spool/mixmaster>.

=item A per-user systemd path service in C<~./config/systemd/user>
      that watches for changes to the spool directory and performs
      builds.

=end pod

our Str constant SCRIPT_VERSION = "2020.04.21";

sub createBuildRoot(IO::Path $path) {
    try {
        mkdir($path);
        say "Created {$path} directory.";

        CATCH {
            when X::IO::Mkdir {
                die("Unable to create {$path}");
            }
        }
    }
}

sub createConfigurationFile(
    IO::Path $configPath,
    IO::Path $buildRoot,
    IO::Path $spool,
    Str $email
) {
    spurt $configPath, qq:to/END/;
    ; This is the configuration file for mixmaster. It maps project repositories
    ; to build commands and defines application settings.

    [_]

    ; The filesystem path to the directory that will store builds.
    buildRoot = {$buildRoot}

    ; The filesystem path to the directory that stores incoming build requests.
    ; The mmbridge script will write files here, and the systemd path service
    ; will watch it for changes.
    spool = {$spool}

    ; Use "dryrun" to have mixmaster echo build commands for testing purposes.
    ; Use "normal" to have build commands executed.
    mode = normal

    ; The email address to send notifications of build progress.
    mailto = {$email}

    ; Example configuration for a project with two buildable targets (branches).
    ;
    ; The production target is built by invoking the command "make deploy".
    ; The staging target is built by invoking "make deploy-to-stage".
    [example-org/example-repo]
    production = make deploy
    staging = make deploy-to-staging

    END

    say "Populated {$configPath} with  default configuration."
}

sub createSystemdServices(IO::Path $root, IO::Path $buildRoot, Int $port) {
    # Bridge socket
    my $bridgeSocket = $root.add("mixmaster-bridge.socket");

    spurt $bridgeSocket, qq:to/END/;
    [Unit]
    Description=Mixmaster Bridge Socket

    [Socket]
    ListenStream={$port}
    Accept=yes
    ReusePort=true

    [Install]
    WantedBy = sockets.target

    # Local Variables:
    # mode: conf
    # End:

    END

    say("Populated {$bridgeSocket}.");

    # Bridge service
    my $bridgeService = $root.add('mixmaster-bridge@.service');

    spurt $bridgeService, qq:to/END/;
    [Unit]
    Description=Mixmaster Bridge Service

    [Service]
    StandardInput=socket
    StandardError=journal
    ExecStart=/usr/bin/rakudo /usr/local/bin/mmbridge

    # Local Variables:
    # mode: conf
    # End:

    END
    say ("Populated {$bridgeService}.");

    # Path watcher
    my $pathWatcher = $root.add("mixmaster-watcher.path");

    spurt $pathWatcher, qq:to/END/;
    [Unit]
    Description=Mixmaster inbox path watcher

    [Path]
    DirectoryNotEmpty=/var/spool/mixmaster/{$*USER}
    MakeDirectory=true
    DirectoryMode=0700

    [Install]
    WantedBy=multi-user.target

    # Local Variables:
    # mode: conf
    # End:

    END
    say ("Populated {$pathWatcher}.");

    # Path service
    my $pathService = $root.add("mixmaster-watcher.service");

    spurt $pathService, qq:to/END/;
    [Unit]
    Description=Mixmaster inbox path service

    [Service]
    WorkingDirectory={$buildRoot}
    ExecStart=/usr/bin/rakudo /usr/local/bin/mmbuild

    # Local Variables:
    # mode: conf
    # End:

    END

    say("Populated {$pathService}.");

}

multi sub MAIN(
    Str  :$buildRoot = "{$*HOME}/Builds", #= Filesystem path for storing builds. Default: ~/Builds
    Str  :$spool = "/var/spool/mixmaster/{$*USER}", #= Filesystem path for storing jobs. Default: /var/spool/mixmaster/[user]
    Str  :$email = '',                    #= Email address to use for notifiations. Default: no email notifications.
    Int  :$port = 8585,                   #= Port to use with bridge service. Default: 8585.
    Bool :$dump,                          #= Display the configuration file.
    Bool :$version,                       #= Display version information.
    Bool :$force,                         #= Overwrite existing files.
    Bool :y(:$yes)                        #= Skip confirmation.
) {
    my IO::Path $configPath = $*HOME.IO.add(".config/mixmaster.ini");
    my IO::Path $resolvedBuildRoot = $buildRoot.IO.resolve;
    my IO::Path $resolvedSpool = $spool.IO.resolve;
    my IO::Path $systemdRoot = IO::Path.new("{$*HOME}/.config/systemd/user");

    if ($version) {
        say SCRIPT_VERSION;
        exit;
    }

    if ($dump) {
        say("Dumping {$configPath}");
        say("-" x 72 ~ "\n");

        .say for $configPath.lines;
        exit;
    }

    my @tasks = ();

    # Build root
    if ($resolvedBuildRoot.d) {
        say "✓ Build root {$resolvedBuildRoot} exists.";
    } else {
        unless ($resolvedBuildRoot.parent.w) {
            die("The parent of {$resolvedBuildRoot} is not writable.\n")
        }

        say("Builds will be stored in {$resolvedBuildRoot}");
        @tasks.push("buildRoot");
    }

    # Config
    if ($configPath.f and not $force) {
        say "✓ Configuration file {$configPath} exists.";
    } else {
        say("The configuration file will be written to {$configPath}");
        @tasks.push("configurationFile");
    }

    # Systemd
    if ($systemdRoot.add("mixmaster-bridge.socket").e and not $force) {
        say "✓ Systemd services exist under {$systemdRoot}.";
    } else {
        # Port
        say "The systemd socket service will use port {$port}";
        say("4 systemd service files wil be written to {$systemdRoot}");
        @tasks.push("systemdServices");
    }

    if (@tasks and not $yes) {
        my Str $confirmation = prompt("Proceed? [y/N] ");
        exit unless $confirmation.lc eq "y";
    }

    if (@tasks and @tasks.contains("buildRoot")) {
        createBuildRoot($resolvedBuildRoot);
    }

    if (@tasks and @tasks.contains("configurationFile")) {
        createConfigurationFile($configPath, $resolvedBuildRoot, $resolvedSpool, $email);
    }

    if (@tasks and @tasks.contains("systemdServices")) {
        createSystemdServices($systemdRoot, $resolvedBuildRoot, $port);
    }

    # Spool
    if not ($resolvedSpool.d) {
        say("❗ The spool directory must be created manually. For example:");
        say("   sudo mkdir -p {$resolvedSpool}");
        say("   sudo chown {$*USER} {$resolvedSpool}");
    } else {
        say "✓ The spool directory {$resolvedSpool} exists.";
    }

    CATCH {
        default {
            .payload.say
        }
    }
}
