#!/usr/bin/env rakudo

constant SCRIPT_VERSION = "dev";
constant DEFAULT_BUILDROOT = "{$*HOME}/Builds";

use lib $*PROGRAM.absolute.IO.parent(2).add("lib");

#| Establish a buildroot and starter config
multi sub MAIN("setup", Str :$buildroot = DEFAULT_BUILDROOT #= Filesystem path where builds will be kept
) {
    run-command("setup", $buildroot.IO);
}

#| Install systemd services
multi sub MAIN("service", Str :$buildroot = DEFAULT_BUILDROOT) {
    run-command("service", $buildroot.IO);
}

#| Receive build requests over HTTP
multi sub MAIN("bridge", Str :$buildroot = DEFAULT_BUILDROOT) {
    run-command("bridge", $buildroot.IO);
}

#| Start a build
multi sub MAIN("build", Str :$buildroot = DEFAULT_BUILDROOT) {
    run-command("build", $buildroot.IO);
}

#| Show the commands a build would run
multi sub MAIN("recipe", Str :$job #= Filesystem path to an existing job file
) {
    run-command("recipe", $job.IO);
}

#| Print the application version
multi MAIN("version") {
    say SCRIPT_VERSION;
}

sub run-command(Str $command, *@args) {
    require ::("Command::{$command.tc}") <&make-it-so>;
    make-it-so(|@args);
}

# Local Variables:
# mode: raku
# End:
