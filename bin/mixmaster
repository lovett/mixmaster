#!/usr/bin/env rakudo

constant SCRIPT_VERSION = "dev";
constant DEFAULT_BUILDROOT = "{$*HOME}/Builds";

use lib "/usr/local/share/mixmaster/lib";
use lib $*PROGRAM.absolute.IO.parent(2).add("lib");

#| Establish a buildroot and starter config
multi sub MAIN("setup", Str :$buildroot = DEFAULT_BUILDROOT #= Filesystem path where builds will be kept
) {
    use Command::Setup;
    Command::setup($buildroot.IO);
}

#| Install systemd services
multi sub MAIN("service", Str :$buildroot = DEFAULT_BUILDROOT) {
    use Command::Service;
    Command::service($buildroot.IO);
}

#| Receive build requests over HTTP
multi sub MAIN("bridge", Str :$buildroot = DEFAULT_BUILDROOT) {
    use Command::Bridge;
    Command::bridge($buildroot.IO);
}

#| Start a build
multi sub MAIN("build", Str :$buildroot = DEFAULT_BUILDROOT) {
    use Command::Build;
    Command::build($buildroot.IO);
}

#| Show the commands a build would run
multi sub MAIN("recipe", Str :$job #= Filesystem path to an existing job file
) {
    use Command::Recipe;
    Command::recipe($job.IO);
}

#| Print the application version
multi MAIN("version") {
    say SCRIPT_VERSION;
}

# Local Variables:
# mode: raku
# End:
